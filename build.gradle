/**
 * build.gradle
 * Butler
 */

ext {
    verbose = false
}

if (System.env.JENKINS_URL) {
    println "Running on jenkins"
    ext.verbose = true
}

/* Bundler */

task bundleInstall(type: Exec) {
    String command = 'bundle install'

    if (!verbose) {
        command += ' --quiet'
    }

    commandLine command.tokenize()
}

/* CocoaPods */

task podLibLint(type: Exec) {
    dependsOn bundleInstall
    commandLine 'bundle exec pod lib lint'.tokenize()
}

/* Swift Package Manager */

task xcodeGenerate(type: Exec) {
    description 'Generates an Xcode project'
    commandLine 'swift package generate-xcodeproj'.tokenize()
}

task spmDescribe(type: Exec) {
    commandLine 'swift package describe'.tokenize()
}

task spmDependencies(type: Exec) {
    commandLine 'swift package show-dependencies'.tokenize()
}

task swiftBuild(type: Exec) {
    commandLine 'swift build'.tokenize()
}

task swiftTest(type: Exec) {
    commandLine 'swift test'.tokenize()
}

/* Top-Level Tasks for CI */

task cocoapods {
    dependsOn podLibLint
}

task spm {
    dependsOn spmDescribe
    dependsOn spmDependencies
    dependsOn swiftBuild
    dependsOn swiftTest
}

task buildAll {
    dependsOn cocoapods
    dependsOn spm
}
