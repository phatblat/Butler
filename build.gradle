/**
 * build.gradle
 * Butler
 */

buildscript {
    repositories {
        maven { url('http://repository.openbakery.org/') }
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.openbakery', name: 'xcode-plugin', version: '+'
    }
}
apply plugin: 'org.openbakery.xcode-plugin'

xcodebuild {
    type = 'macOS'
    target = 'Butler'
    // https://github.com/openbakery/gradle-xcodePlugin/issues/361
    bundleName = 'Butler'
}

ext {
    if (project.hasProperty('verbose')) {
        verbose = project.verbose.toBoolean()
    } else {
        verbose = false
    }
}

if (System.env.JENKINS_URL) {
    println "Running on jenkins"
    ext.verbose = true
}

/* Bundler */

task bundleInstall(type: Exec) {
    String command = 'bundle install'
    if (!verbose) {
        command += ' --quiet'
    }
    commandLine command.tokenize()
}

/* CocoaPods */

task podLibLint(type: Exec) {
    dependsOn bundleInstall

    String command = 'bundle exec pod lib lint'
    if (verbose) {
        command += ' --verbose'
    }
    commandLine command.tokenize()
}

/* Swift Package Manager */

task xcodeGenerate(type: Exec) {
    description 'Generates an Xcode project'
    commandLine 'swift package generate-xcodeproj'.tokenize()
}

task spmDescribe(type: Exec) {
    commandLine 'swift package describe'.tokenize()
}

task spmDependencies(type: Exec) {
    commandLine 'swift package show-dependencies'.tokenize()
}

task swiftBuild(type: Exec) {
    String command = 'swift build'
    if (verbose) {
        command += ' --verbose'
    }
    commandLine command.tokenize()
}

task swiftTest(type: Exec) {
    String command = 'swift test'
    if (verbose) {
        command += ' --verbose'
    }
    commandLine command.tokenize()
}

/* Top-Level Tasks for CI */

task cocoapods {
    dependsOn podLibLint
}

task spm {
    dependsOn spmDescribe
    dependsOn spmDependencies
    dependsOn swiftBuild
    dependsOn swiftTest
}

task buildAll {
    dependsOn cocoapods
    dependsOn spm
}
